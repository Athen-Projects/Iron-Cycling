extensions [
  csv
  py
]

breed [ fe3reducer fe3reducers ]
; breed [ fe3reducerB fe3reducersB ]  ;; possibility to split into fe3reducer (A) and fe3reducerB
breed [ fe2oxidizer fe2oxidizers ]
breed [ e-shuttle e-shuttles ]

turtles-own [
  energy ;; fitness of each individual
  in-biofilm? ;; True if they are part of a biofilm, False if they are not
  biofilm-cooldown ;; How long are bacteria unable to get back into a biofilm after they left
]

patches-own [
  fe?   ;; Boolean, True if the field contains iron
  redox-balance   ;; negative values if there has been more reduction on this field (Fe2+), positive values if more oxidation (Fe3+)
  cluster-identifier   ;; float identifier (x assigned randomly with 0 <= x < 1) shared by iron containing patches of the same cluster, 1 if they don`t contain iron
  p-oxygen ;; oxygen-concentration at the patch (SPECIFY THE UNIT)
  gap? ;;boolean, true if patch is gap between two fe clusters
  pocket? ;; true if patch is surrounded only by fe or gaps
  pocket-id ;; between [0, 1)
]

globals [
  max-ticks  ;; maximum duration of the simulation

  ;; single descriptive values for plotting
  global-mean-energy-fe3reducer
  global-mean-energy-fe2oxidizer
  global-reduction-count  ;; count the number of reduction reactions
  global-oxidation-count  ;; count the number of oxidation reactions
  global-redox-count ;; global redox balance

  oxygen-csv ;; reads input file
  oxy-switch-ticks ;; ticks at which the oxygen concentration changes
  oxy-switch-values ;; new values for the oxygen concentration

  degradation-prob ;;probability that e-shuttle is degraded each time-step

  selected-ticks ;;for bacteria-per-layer-plot, contains ticks at which plot should draw a line
  bacteria-per-layer-plt ;;list of bacteria-per-layer for each selected tick
  fe3reducers-per-layer-plt
  fe2oxidizers-per-layer-plt
  eshuttles-per-layer-plt
  redox-balance-per-layer-plt

  gap-dist ;; distance a gap can have to an fe patch
  shuttle-transfer-count ;;counts number of electron transfers via e shuttles per tick
  fe3reducers-on-fe ;; number of fe3reducers on fe patches
]

to setup
  clear-all
  make-iron-patches
  make-bacteria
  reset-ticks
  set global-reduction-count 0
  set global-oxidation-count 0
  update-reporter

  ;;read input file as oxygen-csv, shift ticks into one variable and values into another
  set oxygen-csv (csv:from-file "/home/natalie/netlogo_programs/Iron-Cycling-master/OxygenValues3D.csv" ";")
  set oxy-switch-ticks item 0 (sublist oxygen-csv 0 1)
  set oxy-switch-values sublist oxygen-csv 1 (length oxygen-csv)
  switch-oxygen3D
  set degradation-prob 0.02
  set max-ticks 3900 ;;3900
  set bacteria-per-layer-plt []
  set redox-balance-per-layer-plt []

  let i 0
  let j 0
  let tick-distance max-ticks / 8 ;;needs to be updated according to plot preferences
  set selected-ticks []
  while [j <= max-ticks] [
    set selected-ticks lput (round (i * tick-distance)) selected-ticks
    set i (i + 1)
    set j (j + tick-distance)
  ]
  set gap-dist 3
  ;; pockets
end

to go
  if ( empty? selected-ticks  or ticks > max-ticks ) [ plot-bacteria-per-layer ]
  if not any? turtles [ stop ]
  if ticks = max-ticks + 1 [ stop ]
  set shuttle-transfer-count 0
  ;;set fe3reducers-on-fe 0
  switch-oxygen3D
  fe2-oxidation-by-air

  update-layer-plots

  manage-shuttles

  ;; all actions of shewanella
  ask fe3reducer [
    if (in-biofilm? != True) [
      move-shewanella speed-fe3reducer
    ]

    if ( [ fe? = True ] of patch-here ) [
      set fe3reducers-on-fe fe3reducers-on-fe + 1
    ]
    ifelse ([p-oxygen] of patch-here <= 1) [
      reduce-iron
    ]
    [
      ;; get energy from O2, approximately five times the amount of iron respiration (Kostka2002).
      set energy energy + 5
    ]
  ]

  ;; all actions of sideroxydans
  ask fe2oxidizer [
    if (in-biofilm? != True) [
      move speed-fe2oxidizer
    ]
    ifelse ([p-oxygen] of patch-here >= 1) and ([p-oxygen] of patch-here <= 3) [
      oxidize-iron
    ]
    [
      ;; get energy from alternative electron sink, e.g. nitrate
      set energy energy + 1
    ]
  ]

  ;;manage-biofilms
  update-color
  update-reporter
end

;;;;PATCH PROCEDURES;;;;

;; setup the patches at the beginning of the model
to make-iron-patches
  ;; initialize patches
  ask patches [
    set cluster-identifier 1
    set fe? False
  ]

  ;; create fe-containing patches randomly
  ;; non-clustered fe-patches
  repeat (count patches * (fe-patch-percentage / 100) * (1 - fe-clustering / 100)) [
    ifelse any? (patches with [(fe? != True) and
                  (not (any? neighbors with [ fe? = True ]))]) [
      assign-fe False
    ]
    [
      assign-fe True
    ]
  ]

  ;; clustered fe-patches
  repeat (count patches * (fe-patch-percentage / 100) * (fe-clustering / 100)) [
    ifelse any? (patches with [ fe? = True ]) [
      assign-fe True
    ]
    [
      assign-fe False
    ]
  ]

  update-color
  while [ count patches with [(fe? = True) and (cluster-identifier = 1)] > 0]
    [ask n-of 1 (patches with [(fe? = True) and (cluster-identifier = 1)])
      [ set cluster-identifier random-float 1
        group-clusters ]
    ]
end

;; assign iron to the patches, if cluster? = False a new seed for a cluster is created, else the cluster is extended by another patch
to assign-fe [ cluster? ]
  ifelse cluster? = False [
    ask n-of 1 (patches with [(fe? != True) and
                  (not (any? neighbors with [fe? = True]))]) [
      set pcolor red
      set fe? True
      set redox-balance 0
    ]
  ]
  [ ask n-of 1 (patches with [(fe? = True) and ((count neighbors with [(fe? != True) and (distance-nowrap myself = 1)]) > 0)]) [
      ask (last sort-on [count neighbors with [(fe? = True) and (distance-nowrap myself = 1)]] neighbors with [(fe? != True) and (distance-nowrap myself) = 1]) [
        set pcolor red
        set fe? True
        set redox-balance 0
      ]
    ]
  ]
end

;; colors the patches according to the amount of Fe2+ on the surface. The more Fe2+ the darker appears the blue of the patch.
to update-color
  ask patches with [Fe? = True][
    ifelse redox-balance = 0
      [set pcolor red]  ;; purely Fe3+ on surface
      [set pcolor blue + 2 - ((log (-1 * redox-balance + 1) 10) * passivation-factor) * 2]  ;; Fe2+ adherent to surface
  ]
end

;;assigns same cluster-identifier to patches of the same cluster
to group-clusters
  ask neighbors with [(cluster-identifier = 1) and
    (fe? = True) and
    (distancexyz-nowrap ([pxcor] of myself) ([pycor] of myself) ([pzcor] of myself) <= 1.74)]
  [ set cluster-identifier [cluster-identifier] of myself
    group-clusters ]
end

;;fe2 is oxidized through oxygen
to fe2-oxidation-by-air
  ask patches with [(fe? = True) and (redox-balance < 0) and (p-oxygen >= 1)] [
    ;;minimum amount of iron oxidized. E. g. oxidized-iron-int = 4 for redox-balance * chemical-oxidation-factor = -4.5
    let oxidized-iron-int -1 * (ceiling (redox-balance * chemical-oxidation-factor * p-oxygen / 21))
    ;;stochastically oxidize the remaining broken iron. E. g. prob-remaining = 0.5 for redox-balance * chemical-oxidation-factor = -4.5
    let prob-remaining-iron (ceiling (redox-balance * chemical-oxidation-factor)) - (redox-balance * chemical-oxidation-factor)
    set redox-balance redox-balance + oxidized-iron-int
    set global-oxidation-count global-oxidation-count + oxidized-iron-int
    if random-float 1 <= prob-remaining-iron [
      set redox-balance redox-balance + 1
      set global-oxidation-count global-oxidation-count + 1
    ]
  ]
end

to pockets
  gaps

  ask patches with [ fe? = True or gap? = True] [
    set pocket-id 1
    set pocket? False
  ]

  let current-id 0
  let id-list []

  while [count (patches with [ pocket-id = 0 ]) > 0] [
    ask one-of (patches with [ pocket-id = 0 ]) [
      set current-id random-float 1
      set id-list lput current-id id-list
      assign-pocket-patches current-id
    ]
  ]

  if-else  ( length(id-list) > 1 ) [
    identify-pockets id-list
  ] [
    ask patches with [ fe? = False or gap? = False ] [
    set pocket? False
    ]
  ]

end

to gaps
  let cluster-id-list []
  ask patches with [fe? = True][
    set cluster-id-list fput cluster-identifier cluster-id-list
  ]
  set cluster-id-list remove-duplicates cluster-id-list
  loop[
    if ( cluster-id-list = [] ) [
      ask patches with [ gap? = True ] [
      ]
      stop
    ]
    let id last cluster-id-list
    ask patches with [ cluster-identifier = id and any? ( neighbors with [ fe? = False ] ) ] [
      ask neighbors with [ fe? = False and gap? != True and ( distance-nowrap myself < ( gap-dist / 2 ) ) ] [
        let first-cluster-dist distance-nowrap myself
        if ( any? patches with [ fe? = True and distance-nowrap myself < ( gap-dist - first-cluster-dist ) and ( cluster-identifier != id ) ] ) [
          set gap? True
        ]
      ]
    ]
    set cluster-id-list but-last cluster-id-list
  ]

end

to assign-pocket-patches [ ci ]
  set pocket-id ci
  while [count (neighbors with [ pocket-id = 0 ]) > 0] [
    ask one-of (neighbors with [ pocket-id = 0 ]) [
      assign-pocket-patches ci
    ]
  ]
end

to identify-pockets [ id-list ]
  foreach id-list [ x ->
    if-else (any? patches with  [ pocket-id = x and ((member? (round pxcor) (list max-pxcor min-pxcor)) or (member? (round pycor) (list max-pycor min-pycor)) or (member? (round pzcor) (list max-pzcor min-pzcor)) ) ] ) [
      ask patches with [ pocket-id = x ] [
        set pocket? False
      ]
    ][
      ask patches with [ pocket-id = x ] [
        set pocket? True
      ]
    ]
  ]
end

;;;;TURTLE PROCEDURES;;;;

;; setup the bacteria at the beginning of the model
to make-bacteria
  ;; Shewanella
  create-fe3reducer initial-number-fe3reducer [
    set color blue
    set size 1
    set in-biofilm? False
    set biofilm-cooldown 0
    set energy 10
    let destiny (one-of (patches with [fe? != True]))
    setxyz ([pxcor] of destiny) ([pycor] of destiny) ([pzcor] of destiny)
    set roll random 360
    tilt-up random 360
  ]

  ;; Sideroxydans
  create-fe2oxidizer initial-number-fe2oxidizer [
    set color red
    set size 1
    set in-biofilm? False
    set biofilm-cooldown 0
    set energy 10
    let destiny (one-of (patches with [fe? != True]))
    setxyz ([pxcor] of destiny) ([pycor] of destiny) ([pzcor] of destiny)
    set roll random 360
    tilt-up random 360
  ]
end

to update-layer-plots
  if member? ticks selected-ticks [
    show ticks
    let i max-pzcor
    let bacteria-per-layer []
    let redox-balance-per-layer []
    let fe3reducers-per-layer []
    let fe2oxidizers-per-layer []
    let eshuttles-per-layer []

    let help1 0
    let help2 0
    while [ i > ( min-pzcor - 1 ) ]
    [
      set help1 sum [ redox-balance ] of patches with [ ( round pzcor ) = i and ( count neighbors with [ fe? = false ] ) > 0 ]
      set help2 count patches with [ ( round pzcor ) = i and ( count neighbors with [ fe? = false ] > 0 ) ]

      set redox-balance-per-layer lput ( help1 / help2 ) redox-balance-per-layer
      set bacteria-per-layer lput ( count ( fe3reducer with [ ( round zcor ) = i ] ) + count ( fe2oxidizer with [ ( round zcor ) = i ] ) ) bacteria-per-layer
      set fe3reducers-per-layer lput count ( fe3reducer with [ (round zcor) = i ]) fe3reducers-per-layer
      set fe2oxidizers-per-layer lput count ( fe2oxidizer with [ (round zcor) = i ]) fe2oxidizers-per-layer
      set eshuttles-per-layer lput count ( e-shuttle with [ (round zcor) = i ]) eshuttles-per-layer
      set i ( i - 1 )
    ]

    set bacteria-per-layer-plt lput bacteria-per-layer bacteria-per-layer-plt
    set redox-balance-per-layer-plt lput redox-balance-per-layer redox-balance-per-layer-plt
    set fe3reducers-per-layer-plt lput fe3reducers-per-layer fe3reducers-per-layer-plt
    set fe2oxidizers-per-layer-plt lput fe2oxidizers-per-layer fe2oxidizers-per-layer-plt
    set eshuttles-per-layer-plt lput eshuttles-per-layer eshuttles-per-layer-plt

  ]
end

to plot-bacteria-per-layer
  ;; plot bacteria per layer
  ;;set variables from netlogo
  py:setup py:python3
  py:set "redoxBalancePerLayer" redox-balance-per-layer-plt ;;x axis
  py:set "bacteriaPerLayer" bacteria-per-layer-plt ;;x axis
  py:set "fe3reducerPerLayer" fe3reducers-per-layer-plt
  py:set "fe2oxidizerPerLayer" fe2oxidizers-per-layer-plt
  py:set "eshuttlesPerLayer" eshuttles-per-layer-plt
  py:set "zcoordinates" ( range min-pzcor ( max-pzcor + 1 ) ) ;; y axis
  py:set "subplotNumber" length bacteria-per-layer-plt ;;# of subplots; goes for both plot lists
  py:set "selectedTicks" selected-ticks
  py:set "bacteriaCount" initial-number-fe3reducer + initial-number-fe2oxidizer
  py:set "maxZcor" max-pzcor
  py:set  "minZcor" min-pzcor
  py:set "nrows" 0
  py:set "ncols" 0
 (py:run
    ;;import packages and declare locals
    "import math"
    "import matplotlib"
    "import numpy as np"
    "import matplotlib.pyplot as plt"
    "l = 0"
    ;;compute number of rows and columns
    "def subplot_dimensions(subplotNumber):"
    "    if subplotNumber < 4:"
    "        return subplotNumber, 1"
    "    elif subplotNumber < 11:"
    "        return math.ceil(subplotNumber/2), 2"
    "    else:"
    "        return math.ceil(subplotNumber/3), 3"
    "ncols, nrows = subplot_dimensions(subplotNumber)"

    ;;plots bacteria-per-layer (create all subplots and delete empty ones)
    "fig, axs = plt.subplots(nrows, ncols, squeeze=True)"
    "for i in range (0, nrows):"
    "    for j in range(0, ncols):"
    "        if l < len(bacteriaPerLayer):"
    "            axs[i, j].plot(bacteriaPerLayer[l], zcoordinates, color='indianred')"
    "            axs[i, j].set_title('tick '+ str(selectedTicks[l]))"
    "        else:"
    "            fig.delaxes(axs[i, j])"
    "        l += 1"
    ;;set names of x and y axes, just label the outer axes
;    "for ax in fig.get_axes():"
;    "    ax.set_xticks(np.arange(0, bacteriaCount, step=int(bacteriaCount/4)))"
;    "    ax.set_yticks(np.arange(-15, 16, step=5))"
;    "    ax.label_outer()"
    "axs[0, 0].set(ylabel='z-layer')"
    "axs[nrows - 1, 0].set(xlabel='number of bacteria')"
    "plt.show()"

    ;;plots redox-balance-per-layer
    "l = 0"
    "figure, axes = plt.subplots(nrows, ncols, squeeze=True)"
    "for m in range (0, nrows):"
    "    for n in range(0, ncols):"
    "        if l < len(redoxBalancePerLayer):"
    "            axes[m, n].plot(redoxBalancePerLayer[l], zcoordinates, color='seagreen')"
    "            axes[m, n].set_title('tick '+ str(selectedTicks[l]))"
    "        else:"
    "            figure.delaxes(axes[m, n])"
    "        l += 1"
    ;;set names of axes
;    "for ax in figure.get_axes():"
;    "    ax.label_outer()"
    "axes[0, 0].set(ylabel='z-layer')"
    "axes[nrows - 1, 0].set(xlabel='redox balance per layer')"
    "plt.show()"
  )
end

;; all electron-shuttle related actions
to manage-shuttles
  if (count e-shuttle) > 0 [
    ask e-shuttle [
      ;; degraded?
      if (random-float 1) < degradation-prob [
        die
      ]

      ;; movement
      move 2

      ;; electron transfer upon contact with Fe3+
      ifelse (([fe?] of patch-here) = True) and (color = blue) [
        if (random-float 1) > ((log ([-1 * redox-balance + 1] of patch-here) 10) * passivation-factor) [
          ;; only when there is low Fe2+ on the surface, Fe3+ can be reduced to Fe2+
          ask patch-here [
            set redox-balance redox-balance - 1
          ]
          set color red
          set global-reduction-count global-reduction-count + 1
          set shuttle-transfer-count shuttle-transfer-count + 1
        ]
      ]
      ;; upon contact, let Shewanella refill the electron-shuttle with an electron
      [
        if (any? (fe3reducer-on patch-here)) and (color = red) [
          ask one-of (fe3reducer-on patch-here) [
            set energy energy + 1
          ]
          set color blue
        ]
      ]
    ]
  ]

  ;; produce e-shuttle
  ask (fe3reducer with [(in-biofilm? = False) and (([fe?] of patch-here) != True) and (count (e-shuttle in-radius 2) <= 3)]) [
    if energy > 3 [
      hatch-e-shuttle 1 [
        set shape "dot"
        set size 0.5
        set color blue
        set roll random 360
        tilt-up random 360
      ]
      set energy energy - 3
    ]
  ]
end

to move [ speed ]  ;; move turtles around randomly ;; let them stop if a wall is ahead
  set roll random 360
  tilt-up random 360
  ifelse (([any? (neighbors6 with [fe? != True])] of patch-ahead speed = True) and
  (distancexyz-nowrap ([pxcor] of patch-ahead speed) ([pycor] of patch-ahead speed) ([pzcor] of patch-ahead speed) <= 1.74 * speed))
  [
    fd speed
  ]
  [
    let i 1
    while [not (([any? (neighbors6 with [fe? != True])] of patch-ahead (speed - i) = True) and
    (distancexyz-nowrap ([pxcor] of patch-ahead (speed - i)) ([pycor] of patch-ahead (speed - i)) ([pzcor] of patch-ahead (speed - i)) <= 1.74 * (speed - i)))] [
      ifelse ( speed - (i + 1) > 0)[
        set i (i + 1)
      ]
      [
        stop
      ]
    ]
    fd (speed - i)
  ]
end

to move-shewanella [ speed ]  ;; move turtles around randomly ;; let them stop if a wall is ahead
  ;; if patch-here has fe the turtle will reverse with probability 1, if not it will reverse with probability of 1/3
  ifelse ((any? ((patch-set patch-here neighbors) with [fe? = True])) and
          (mean [redox-balance] of ((patch-set patch-here neighbors) with [fe? = True]) >= (-0.5 / passivation-factor)))
  [
    set roll random 360
    tilt-up random 360
    ifelse (([any? (neighbors6 with [fe? != True])] of patch-ahead speed = True) and
    (distancexyz-nowrap ([pxcor] of patch-ahead speed) ([pycor] of patch-ahead speed) ([pzcor] of patch-ahead speed) <= 1.74 * speed))
    [
      fd speed
    ]
    [
      let i 1
      while [not (([any? (neighbors6 with [fe? != True])] of patch-ahead (speed - i) = True) and
      (distancexyz-nowrap ([pxcor] of patch-ahead (speed - i)) ([pycor] of patch-ahead (speed - i)) ([pzcor] of patch-ahead (speed - i)) <= 1.74 * (speed - i)))] [
        ifelse ( speed - (i + 1) > 0)[
          set i (i + 1)
        ]
        [
          stop
        ]
      ]
      fd (speed - i)
    ]
  ]
  [
    if random-float 1 <= (1 / 3) [
      set roll random 360
      tilt-up random 360
    ]
    ifelse (([any? (neighbors6 with [fe? != True])] of patch-ahead speed = True) and
    (distancexyz-nowrap ([pxcor] of patch-ahead speed) ([pycor] of patch-ahead speed) ([pzcor] of patch-ahead speed) <= 1.74 * speed))
    [
      fd speed * 0.5
    ]
    [
      let i 1
      while [not (([any? (neighbors6 with [fe? != True])] of patch-ahead (speed - i) = True) and
      (distancexyz-nowrap ([pxcor] of patch-ahead (speed - i)) ([pycor] of patch-ahead (speed - i)) ([pzcor] of patch-ahead (speed - i)) <= 1.74 * (speed - i)))] [
        ifelse ( speed - (i + 1) > 0)[
          set i (i + 1)
        ]
        [
          stop
        ]
      ]
      fd (speed - i)
    ]
  ]
end

to reduce-iron
  if [ fe? = True ] of patch-here [
    ;; Shewanella uses O2 as an electron sink
    ;; only when O2 is missing, it reduces Fe3 to Fe2 to gain energy
    if (random-float 1) > ((log ([-1 * redox-balance + 1] of patch-here) 10) * passivation-factor) [
    ;; only when there is no Fe2+ on the surface, Fe3+ can be reduced to Fe2+
      ask patch-here [
        set redox-balance redox-balance - 1
      ]
      set energy energy + 1
      if show-labels? [ set label "R" ]
      set global-reduction-count global-reduction-count + 1
    ]
  ]
end

to oxidize-iron
  if [redox-balance] of patch-here < 0 [
    ;; Sideroxydans uses O2 as energy source
    ;; only between 18-20% oxidizes Fe2 to Fe3
    ask patch-here [
      ifelse (redox-balance > -10) [
        set global-oxidation-count global-oxidation-count - redox-balance
      ]
      [
        set global-oxidation-count global-oxidation-count + 10
      ]
      set redox-balance (min (list (redox-balance + 10) 0))
    ]
    set energy energy + 50
    if show-labels? [ set label "O" ]
  ]
end

;; create, expand or resolve biofilms.
to manage-biofilms
  ;; only adress iron-cluster patches.
  let unique-clusters remove-duplicates [cluster-identifier] of patches with [cluster-identifier < 1]
  ;; adress every cluster.
  foreach unique-clusters [ x ->
    ;; bacteria who still have to be adressed
    let bacteria-to-consider (list ((fe3reducer-on (patches with [cluster-identifier = x])) with [biofilm-cooldown < ticks]))
    ;; bacteria in the biofilm
    if any? (turtle-set bacteria-to-consider) with [in-biofilm? = True] [
      ask (turtle-set bacteria-to-consider) with [in-biofilm? = True] [
        if (in-biofilm? = True) [
          ;; is the passivation high enough for the biofilm to start resolving?
          if ((mean ([redox-balance] of (patch-set patch-here ([patch-here] of link-neighbors)))) * passivation-factor * -1 >= biofilm-dissolve-treshold) [
            ;; if the biofilm is already quite small, release all the bacteria.
            ifelse (count link-neighbors <= 3)[
              set in-biofilm? False
              set biofilm-cooldown (ticks + 30)
              set bacteria-to-consider remove self bacteria-to-consider
              ask link-neighbors [
                set in-biofilm? False
                set biofilm-cooldown (ticks + 30)
                set bacteria-to-consider remove self bacteria-to-consider
                ask my-links [die]
              ]
            ]
            [
              ;; if there are more bacteria in the biofilm, ask every single one to escape the biofilm with
              ;; the probability biofilm-release-probability
              if (random-float 1 <= biofilm-release-probability) [
                set in-biofilm? False
                set biofilm-cooldown (ticks + 30)
                set bacteria-to-consider remove self bacteria-to-consider
                ask my-links [die]
              ]
            ]
          ]
        ]
      ]
    ]
    ;; bacteria on the cluster, but not in a biofilm
    if any? ((turtle-set bacteria-to-consider) with [in-biofilm? = False]) [
      ask (turtle-set bacteria-to-consider) with [in-biofilm? = False] [
        if (in-biofilm? = False) [
          ;; if there is already a biofilm close to the bacteria considered, recruit it with biofilm-recruitment-probability
          ifelse any? ((fe3reducer-on (patches with [cluster-identifier = x])) with [(in-biofilm? = True) and (distance myself <= 2)]) [
            let friend-who-invited-me min-one-of ((fe3reducer-on (patches with [cluster-identifier = x])) with [(in-biofilm? = True) and (distance myself <= 2)]) [distance myself]
            if (random-float 1 <= biofilm-recruitment-probability) [
              create-links-with (turtle-set friend-who-invited-me ([link-neighbors] of friend-who-invited-me))
              set in-biofilm? True
              set bacteria-to-consider remove self bacteria-to-consider
              move-to (min-one-of ((([neighbors] of friend-who-invited-me) with [fe? = True]) with-min [count fe3reducer-here]) [distance myself])
              ]
          ]
          [
            ;; if enough unbound bacteria are close, the cluster is big enough and the passivation is not too high, found a new biofilm
            if ((count ((turtle-set bacteria-to-consider) with [(distance myself <= 2) and (in-biofilm? = False)]) >= 3) and
              (count (patch-set patch-here (neighbors with [fe? = True])) >= 4) and
              (mean ([redox-balance] of (patch-set patch-here (neighbors with [fe? = True]))) * passivation-factor * -1 <= biofilm-start-treshold)) [
                set in-biofilm? True
                set bacteria-to-consider remove self bacteria-to-consider
                let biofilm-starter self
                ask ((turtle-set bacteria-to-consider) with [(distance biofilm-starter <= 2) and (in-biofilm? = False)]) [
                  create-links-with (turtle-set biofilm-starter ([link-neighbors] of biofilm-starter))
                  set in-biofilm? True
                  set bacteria-to-consider remove self bacteria-to-consider
                  move-to (one-of ((([neighbors] of biofilm-starter) with [fe? = True]) with-min [count fe3reducer-here]))
                ]
            ]
          ]
        ]
      ]
    ]
  ]
end

;;;; OTHER PROCEDURES ;;;;

;;switches oxygen for each xy-plane of all z-coordinates separately.
to switch-oxygen3D
  if (member? ticks oxy-switch-ticks) = True [
    foreach oxy-switch-values [ x ->
      ask patches with [pzcor = (item 0 x) - max-pzcor] [
        set p-oxygen item (position ticks oxy-switch-ticks) x
      ]
    ]
  ]
end

;;update plots
to update-reporter
  ;; update global counter for plots
  ifelse any? fe3reducer
  [ set global-mean-energy-fe3reducer mean [ energy ] of fe3reducer ]
  [ set global-mean-energy-fe3reducer 0 ]

  ifelse any? fe2oxidizer
  [ set global-mean-energy-fe2oxidizer mean [ energy ] of fe2oxidizer ]
  [ set global-mean-energy-fe2oxidizer 0]

  ifelse any? turtles and ticks > 0
  [
    set global-redox-count global-reduction-count - global-oxidation-count
  ]
  [
    set global-redox-count 0
  ]
end
@#$#@#$#@
GRAPHICS-WINDOW
0
0
437
438
-1
-1
13.0
1
10
1
1
1
0
1
1
1
-16
16
-16
16
-16
16
1
1
1
ticks
30.0

BUTTON
56
39
119
72
setup
setup
NIL
1
T
OBSERVER
NIL
NIL
NIL
NIL
1

SLIDER
260
423
441
456
fe-patch-percentage
fe-patch-percentage
0
100
4.0
1
1
%
HORIZONTAL

SLIDER
260
467
441
500
fe-clustering
fe-clustering
0
100
90.0
1
1
%
HORIZONTAL

BUTTON
130
39
193
72
go
go\ntick
T
1
T
OBSERVER
NIL
NIL
NIL
NIL
0

SLIDER
260
511
441
544
passivation-factor
passivation-factor
0
1
0.2
0.05
1
NIL
HORIZONTAL

INPUTBOX
69
493
224
553
initial-number-fe2oxidizer
0.0
1
0
Number

INPUTBOX
69
423
224
483
initial-number-fe3reducer
200.0
1
0
Number

PLOT
211
53
540
371
Redox-balance
time
Surplus of reductions
0.0
1000.0
0.0
1000.0
true
true
"" ""
PENS
"default" 1.0 0 -16777216 true "" "plot global-redox-count"

TEXTBOX
85
397
235
415
Bacteria related settings
11
0.0
1

TEXTBOX
307
396
457
414
Iron related settings
11
0.0
1

TEXTBOX
492
395
642
413
Environment related settings
11
0.0
1

SLIDER
474
423
668
456
chemical-oxidation-factor
chemical-oxidation-factor
0
1
0.28
0.01
1
NIL
HORIZONTAL

SWITCH
56
89
193
122
show-labels?
show-labels?
0
1
-1000

SLIDER
69
599
224
632
speed-fe2oxidizer
speed-fe2oxidizer
0
1
0.19
0.01
1
NIL
HORIZONTAL

SLIDER
69
560
224
593
speed-fe3reducer
speed-fe3reducer
0
1
0.91
0.01
1
NIL
HORIZONTAL

TEXTBOX
732
394
882
412
Biofilm related settings
11
0.0
1

SLIDER
700
423
886
456
biofilm-start-treshold
biofilm-start-treshold
0
1
0.1
0.05
1
NIL
HORIZONTAL

SLIDER
700
466
887
499
biofilm-dissolve-treshold
biofilm-dissolve-treshold
0
1
0.65
0.05
1
NIL
HORIZONTAL

SLIDER
700
508
888
541
biofilm-release-probability
biofilm-release-probability
0
1
0.5
0.05
1
NIL
HORIZONTAL

SLIDER
700
554
888
587
biofilm-recruitment-probability
biofilm-recruitment-probability
0
1
0.6
0.05
1
NIL
HORIZONTAL

PLOT
584
53
911
372
Energy
time
energy-level
0.0
400.0
0.0
50.0
true
false
"" ""
PENS
"energy fe3reducer" 1.0 0 -13791810 true "" "if count fe3reducer > 0 [plot global-mean-energy-fe3reducer]"
"energy fe2oxidizer" 1.0 0 -2674135 true "" "if count fe2oxidizer > 0 [plot global-mean-energy-fe2oxidizer]"

PLOT
959
53
1159
372
Oxygen profile
oxygen-conc
depth
0.0
25.0
-16.0
16.0
true
false
"" "clear-plot"
PENS
"default" 1.0 0 -16777216 true "" "foreach (range -16 17) [ x -> plotxy (mean ([p-oxygen] of (patches with [pzcor = x]))) x]"

PLOT
1177
53
1562
372
E shuttle electron transfer count
time
electron transfers via e shuttle
0.0
10.0
0.0
10.0
true
false
"" ""
PENS
"transfer count" 1.0 0 -5298144 true "" "plot shuttle-transfer-count"

PLOT
921
422
1124
750
Ticks * fe3 reducers
NIL
NIL
0.0
10.0
0.0
1.0
true
false
"" ""
PENS
"" 1.0 0 -5298144 true "" "plot fe3reducers-on-fe / ( ( count fe3reducer ) * ticks + 1 ) "

PLOT
1145
422
1454
751
Fe3 reducers on fe patches
time
fe3 reducers on fe patches
0.0
10.0
0.0
10.0
true
false
"" ""
PENS
"fe reducers on fe patches" 1.0 0 -5298144 true "" "plot fe3reducers-on-fe"

@#$#@#$#@
# Overview
## Purpose

The purpose of the model is the description of the dynamics in iron reduction (by Shewanella) and iron oxidation (by Sideroxydans). After validation the tripartite interaction with another species (Pseudomonas) ought to be modelled.

## Entities, state variables, and scales

The 3D-environment represents the peat habitat of the bacteria and contains iron clusters.

So far the model includes three types of agents: two of them are the bacteria fe3reducers (Shewanella) and fe2oxidizers (Sideroxydans), and the third represents electron shuttles secreted by Shewanella.


Global variables:
	**max-ticks**: tick count at which the model ends.
	**global-mean-energy-fe3reducer**: the mean energy level of the fe3 reducing				bacteria.
 	**global-mean-energy-fe2oxidizer**: the mean energy level of the fe2 oxidizing 				bacteria.
 	**global-reduction-count**: counts how often iron gets reduced.
 	**global-oxidation-count**: counts how often iron gets oxidized.
 	**global-redox-count**: the difference between global-reduction-count and				global-oxidation-count.
	**oxygen-csv**: stores the information of the csv-file containing the changing 				oxygen profile (experimental measures for oxygen-profiles are retrieved				from **[1]**)
	**oxy-switch-ticks**: ticks at which the oxygen profile changes.
	**oxy-switch-values**: values the oxygen profile gets adjusted to.


Patch variables:
	**fe?**: differentiates iron mineral containing patches [fe? = True] from those				containing no iron minerals [fe? = False].
	**redox-balance**: Indicator for the amount of Fe2+ on the patch. 0 if no fe2+				present, -1 for every model unit (has yet to be defined) of fe2+.
	**cluster-identifier**: iron mineral containing patches adhering together have				the same float identifier.
	**p-oxygen**: concentration of oxygen in atm.


Agent variables:
	**energy**: measure of fitness (specify)
	**in-biofilm?**: True if the agent is in a biofilm, False else.
	**biofilm-cooldown**: time the bacteria is unable to re-enter a biofilm after				leaving.


## Process Overview and Scheduling

Stop the model if one of those conditions is satisfied:
	1. there are no agents left
	2. the runtime (ticks) exceeds the time specified in max-ticks

Changes the oxygen-levels if specified in your csv file

Abiotic iron oxidation

Actions of the e-shuttles:
	1. Live or die
	2. Movement
	3. Electron transfer to Fe3+
	4. Refill electron by fe3reducer if close
	5. Production

Actions of Shewanella:
	1. Movement
	2. Iron reduction

Actions of Sideroxydans:
	1. Movement
	2. Iron oxidation

Update the color of the iron patches

Update the reporters.

# Design concepts

## Basic principles

The basic principle adressed by this model is the influence of oxygen on the reduction/oxidation rates and survival of the different bacteria. As together the respiration processes of the bacteria form a cycle, i.e. they provide each other with the substrates for their respiration, they heavily influence each others lifestyle. By simulating different strategies (for example chemotaxis, adherence to particles) and varying oxygen profiles we want to answer two key questions.

1. What kind of iron aquisition strategies are optimal for efficient respiration?
2. What kind of oxygen profiles result in cycles with the highest throughput of iron?

## Emergence

First and for validation purposes we want the following features to emerge from our model:
1. a realistic vertical distribution of the modelled bacteria **[2]**.
2. Fe2+ concentrations for different oxic/anoxic periods matching those of **[3]**.

## Adaptation

Bacteria adapt to the redox state of the iron mineral surface by changing their speed and frequency of turns. They also base their decision of biofilm formation and release on the redox state of the particle.

## Objectives

-

## Learning Prediction

-

## Sensing

Bacteria are assumed to know the local oxygen concentration (at the patch they inhabit) and the redox state of the surrounding environment (up to a one patch distance).

## Interaction

The bacteria interact indirectly with each other through the production/consumption of Fe2+ and Fe3+. Later we also want to include bacteria influencing local oxygen concentrations. fe3reducers (Shewanella) interact with e-shuttles by producing and transferring electrons to them. They are able to re-reduce the e-shuttles once they are close to each other.

## Stochasticity

The bacteria are randomly positioned in the environment at the beginning of the model. If bacteria and e-shuttles are able to reduce/oxidize iron once they are close is also determined stochastically. The passivation effect implies that it gets more and more unlikely for a reduction event to be successful.

## Collectives 

It is planned to implement biofilms although the different time scales (e.g. compared with chemotaxis) poses still an issue to discuss. Bacteria in biofilms are unable to move and are only able to reduce the patches the biofilm adheres to.

## Observation

We aim to access the validity of the model by comparing the emerging distribution of bacteria and the oscillations of Fe2+ with experimental data. We do the visualization with python as it offers more costumization opportunities compared to the internal visualization options of netlogo itself.

# Details

## Initialization

The model starts by distributing iron containing patches over the environment randomly. The parameter fe-patch-percentage sets the percentage of patches containing iron. fe-clustering gives the percentage of iron containing patches which are integrated into a cluster (at least two iron containing patches connected to each other (6-neighboorhood). passivation-factor sets the strength of the passivation effect (how much harder it gets to reduce Fe3+ once it is covered by a layer of Fe2+) and is constant during a run.

Next the bacteria are created. They are randomly placed in the environment but can not be placed in an iron containing patch. The initial number of bacteria is determined by initial-number-fe3reducer and intial-number-fe2oxidizer, respectively. Their base speed is determined by speed-fe3reducer and speed-fe2oxidizer, respectively.

chemical-oxidation-factor affects the speed of the abiotic oxidation of Fe2+ and degradation-probability sets the chance of an electron shuttle to disappear per tick. Both a constant during a run. max-ticks defines the maximum duration of the model.

## Input data

The model reads input data about the dynamics of vertical oxygen distribution from a csv file, although there are plans to implement oxygen diffusion and from there on only depend on starting values for oxygen concentrations.

## Submodels

### electron-shuttle management

With a certain degradation probability every electron shuttle can vanish each turn. A realistic value for the degradation probability has yet to be defined.

If a reduced electron-shuttle (blue) is at a patch containing iron it may transfer its electron to the iron mineral and hence reducing it. The probability for successful reduction falls in a logarithmic fashion with Fe2+ adhering to the surface of the mineral.

Upon close contact with Shewanella the oxidized electron-shuttle (red) gets re-reduced.

All Shewanella/fe3reducers which are not in a biofilm, do not stand on an iron patch, and have sufficient energy (energy > 3) are asked to produce a reduced electron-shuttle for the cost of 3 energy if there are equal or less than 3 electron shuttles in radius 2. Realistic conditions for electron shuttle production are still to be determined.

### iron reduction by bacteria

all the fe3reducers who stand on an iron patch with an oxygen concentration equal or below 1 % (0.01 atm) have the chance to reduce iron on the spot. The probability for successful reduction falls in a logarithmic fashion with Fe2+ adhering to the surface of the mineral. Upon reduction they gain 1 energy

### iron oxidation by fe2oxidizers

all the fe2oxidizers who stand on an iron patch with an oxygen concentration between 1 % and 3 % oxidize iron on the spot. They oxidize at most 10 units of Fe2+. If there is less Fe2+ on the spot, they oxidize everything. Upon oxidation they gain 50 energy **(This has to be adjusted.)**.

### iron oxidation by air

above a concentration of 1 % iron is oxidized abiotically by oxygen. Oxygen oxidizes **redox-balance * chemical-oxidation-factor * p-oxygen / 21** iron units. 

## References

**[1]** Druschel, G. K., et al. (2008). "Low-oxygen and chemical kinetic constraints on the geochemical niche of neutrophilic iron(II) oxidizing microorganisms." Geochimica et Cosmochimica Acta 72(14): 3358-3370.

**[2]**

**[3]** Barcellos, D., et al. (2018). "Faster redox fluctuations can lead to higher iron reduction rates in humid forest soils." Biogeochemistry 137(3): 367-378.
	
@#$#@#$#@
default
true
0
Polygon -7500403 true true 150 5 40 250 150 205 260 250

airplane
true
0
Polygon -7500403 true true 150 0 135 15 120 60 120 105 15 165 15 195 120 180 135 240 105 270 120 285 150 270 180 285 210 270 165 240 180 180 285 195 285 165 180 105 180 60 165 15

arrow
true
0
Polygon -7500403 true true 150 0 0 150 105 150 105 293 195 293 195 150 300 150

box
false
0
Polygon -7500403 true true 150 285 285 225 285 75 150 135
Polygon -7500403 true true 150 135 15 75 150 15 285 75
Polygon -7500403 true true 15 75 15 225 150 285 150 135
Line -16777216 false 150 285 150 135
Line -16777216 false 150 135 15 75
Line -16777216 false 150 135 285 75

bug
true
0
Circle -7500403 true true 96 182 108
Circle -7500403 true true 110 127 80
Circle -7500403 true true 110 75 80
Line -7500403 true 150 100 80 30
Line -7500403 true 150 100 220 30

butterfly
true
0
Polygon -7500403 true true 150 165 209 199 225 225 225 255 195 270 165 255 150 240
Polygon -7500403 true true 150 165 89 198 75 225 75 255 105 270 135 255 150 240
Polygon -7500403 true true 139 148 100 105 55 90 25 90 10 105 10 135 25 180 40 195 85 194 139 163
Polygon -7500403 true true 162 150 200 105 245 90 275 90 290 105 290 135 275 180 260 195 215 195 162 165
Polygon -16777216 true false 150 255 135 225 120 150 135 120 150 105 165 120 180 150 165 225
Circle -16777216 true false 135 90 30
Line -16777216 false 150 105 195 60
Line -16777216 false 150 105 105 60

car
false
0
Polygon -7500403 true true 300 180 279 164 261 144 240 135 226 132 213 106 203 84 185 63 159 50 135 50 75 60 0 150 0 165 0 225 300 225 300 180
Circle -16777216 true false 180 180 90
Circle -16777216 true false 30 180 90
Polygon -16777216 true false 162 80 132 78 134 135 209 135 194 105 189 96 180 89
Circle -7500403 true true 47 195 58
Circle -7500403 true true 195 195 58

circle
false
0
Circle -7500403 true true 0 0 300

circle 2
false
0
Circle -7500403 true true 0 0 300
Circle -16777216 true false 30 30 240

cow
false
0
Polygon -7500403 true true 200 193 197 249 179 249 177 196 166 187 140 189 93 191 78 179 72 211 49 209 48 181 37 149 25 120 25 89 45 72 103 84 179 75 198 76 252 64 272 81 293 103 285 121 255 121 242 118 224 167
Polygon -7500403 true true 73 210 86 251 62 249 48 208
Polygon -7500403 true true 25 114 16 195 9 204 23 213 25 200 39 123

cylinder
false
0
Circle -7500403 true true 0 0 300

dot
false
0
Circle -7500403 true true 90 90 120

face happy
false
0
Circle -7500403 true true 8 8 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Polygon -16777216 true false 150 255 90 239 62 213 47 191 67 179 90 203 109 218 150 225 192 218 210 203 227 181 251 194 236 217 212 240

face neutral
false
0
Circle -7500403 true true 8 7 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Rectangle -16777216 true false 60 195 240 225

face sad
false
0
Circle -7500403 true true 8 8 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Polygon -16777216 true false 150 168 90 184 62 210 47 232 67 244 90 220 109 205 150 198 192 205 210 220 227 242 251 229 236 206 212 183

fish
false
0
Polygon -1 true false 44 131 21 87 15 86 0 120 15 150 0 180 13 214 20 212 45 166
Polygon -1 true false 135 195 119 235 95 218 76 210 46 204 60 165
Polygon -1 true false 75 45 83 77 71 103 86 114 166 78 135 60
Polygon -7500403 true true 30 136 151 77 226 81 280 119 292 146 292 160 287 170 270 195 195 210 151 212 30 166
Circle -16777216 true false 215 106 30

flag
false
0
Rectangle -7500403 true true 60 15 75 300
Polygon -7500403 true true 90 150 270 90 90 30
Line -7500403 true 75 135 90 135
Line -7500403 true 75 45 90 45

flower
false
0
Polygon -10899396 true false 135 120 165 165 180 210 180 240 150 300 165 300 195 240 195 195 165 135
Circle -7500403 true true 85 132 38
Circle -7500403 true true 130 147 38
Circle -7500403 true true 192 85 38
Circle -7500403 true true 85 40 38
Circle -7500403 true true 177 40 38
Circle -7500403 true true 177 132 38
Circle -7500403 true true 70 85 38
Circle -7500403 true true 130 25 38
Circle -7500403 true true 96 51 108
Circle -16777216 true false 113 68 74
Polygon -10899396 true false 189 233 219 188 249 173 279 188 234 218
Polygon -10899396 true false 180 255 150 210 105 210 75 240 135 240

house
false
0
Rectangle -7500403 true true 45 120 255 285
Rectangle -16777216 true false 120 210 180 285
Polygon -7500403 true true 15 120 150 15 285 120
Line -16777216 false 30 120 270 120

leaf
false
0
Polygon -7500403 true true 150 210 135 195 120 210 60 210 30 195 60 180 60 165 15 135 30 120 15 105 40 104 45 90 60 90 90 105 105 120 120 120 105 60 120 60 135 30 150 15 165 30 180 60 195 60 180 120 195 120 210 105 240 90 255 90 263 104 285 105 270 120 285 135 240 165 240 180 270 195 240 210 180 210 165 195
Polygon -7500403 true true 135 195 135 240 120 255 105 255 105 285 135 285 165 240 165 195

line
true
0
Line -7500403 true 150 0 150 300

line half
true
0
Line -7500403 true 150 0 150 150

pentagon
false
0
Polygon -7500403 true true 150 15 15 120 60 285 240 285 285 120

person
false
0
Circle -7500403 true true 110 5 80
Polygon -7500403 true true 105 90 120 195 90 285 105 300 135 300 150 225 165 300 195 300 210 285 180 195 195 90
Rectangle -7500403 true true 127 79 172 94
Polygon -7500403 true true 195 90 240 150 225 180 165 105
Polygon -7500403 true true 105 90 60 150 75 180 135 105

plant
false
0
Rectangle -7500403 true true 135 90 165 300
Polygon -7500403 true true 135 255 90 210 45 195 75 255 135 285
Polygon -7500403 true true 165 255 210 210 255 195 225 255 165 285
Polygon -7500403 true true 135 180 90 135 45 120 75 180 135 210
Polygon -7500403 true true 165 180 165 210 225 180 255 120 210 135
Polygon -7500403 true true 135 105 90 60 45 45 75 105 135 135
Polygon -7500403 true true 165 105 165 135 225 105 255 45 210 60
Polygon -7500403 true true 135 90 120 45 150 15 180 45 165 90

sheep
false
15
Circle -1 true true 203 65 88
Circle -1 true true 70 65 162
Circle -1 true true 150 105 120
Polygon -7500403 true false 218 120 240 165 255 165 278 120
Circle -7500403 true false 214 72 67
Rectangle -1 true true 164 223 179 298
Polygon -1 true true 45 285 30 285 30 240 15 195 45 210
Circle -1 true true 3 83 150
Rectangle -1 true true 65 221 80 296
Polygon -1 true true 195 285 210 285 210 240 240 210 195 210
Polygon -7500403 true false 276 85 285 105 302 99 294 83
Polygon -7500403 true false 219 85 210 105 193 99 201 83

square
false
0
Rectangle -7500403 true true 30 30 270 270

square 2
false
0
Rectangle -7500403 true true 30 30 270 270
Rectangle -16777216 true false 60 60 240 240

star
false
0
Polygon -7500403 true true 151 1 185 108 298 108 207 175 242 282 151 216 59 282 94 175 3 108 116 108

target
false
0
Circle -7500403 true true 0 0 300
Circle -16777216 true false 30 30 240
Circle -7500403 true true 60 60 180
Circle -16777216 true false 90 90 120
Circle -7500403 true true 120 120 60

tree
false
0
Circle -7500403 true true 118 3 94
Rectangle -6459832 true false 120 195 180 300
Circle -7500403 true true 65 21 108
Circle -7500403 true true 116 41 127
Circle -7500403 true true 45 90 120
Circle -7500403 true true 104 74 152

triangle
false
0
Polygon -7500403 true true 150 30 15 255 285 255

triangle 2
false
0
Polygon -7500403 true true 150 30 15 255 285 255
Polygon -16777216 true false 151 99 225 223 75 224

truck
false
0
Rectangle -7500403 true true 4 45 195 187
Polygon -7500403 true true 296 193 296 150 259 134 244 104 208 104 207 194
Rectangle -1 true false 195 60 195 105
Polygon -16777216 true false 238 112 252 141 219 141 218 112
Circle -16777216 true false 234 174 42
Rectangle -7500403 true true 181 185 214 194
Circle -16777216 true false 144 174 42
Circle -16777216 true false 24 174 42
Circle -7500403 false true 24 174 42
Circle -7500403 false true 144 174 42
Circle -7500403 false true 234 174 42

turtle
true
0
Polygon -10899396 true false 215 204 240 233 246 254 228 266 215 252 193 210
Polygon -10899396 true false 195 90 225 75 245 75 260 89 269 108 261 124 240 105 225 105 210 105
Polygon -10899396 true false 105 90 75 75 55 75 40 89 31 108 39 124 60 105 75 105 90 105
Polygon -10899396 true false 132 85 134 64 107 51 108 17 150 2 192 18 192 52 169 65 172 87
Polygon -10899396 true false 85 204 60 233 54 254 72 266 85 252 107 210
Polygon -7500403 true true 119 75 179 75 209 101 224 135 220 225 175 261 128 261 81 224 74 135 88 99

wheel
false
0
Circle -7500403 true true 3 3 294
Circle -16777216 true false 30 30 240
Line -7500403 true 150 285 150 15
Line -7500403 true 15 150 285 150
Circle -7500403 true true 120 120 60
Line -7500403 true 216 40 79 269
Line -7500403 true 40 84 269 221
Line -7500403 true 40 216 269 79
Line -7500403 true 84 40 221 269

wolf
false
0
Polygon -16777216 true false 253 133 245 131 245 133
Polygon -7500403 true true 2 194 13 197 30 191 38 193 38 205 20 226 20 257 27 265 38 266 40 260 31 253 31 230 60 206 68 198 75 209 66 228 65 243 82 261 84 268 100 267 103 261 77 239 79 231 100 207 98 196 119 201 143 202 160 195 166 210 172 213 173 238 167 251 160 248 154 265 169 264 178 247 186 240 198 260 200 271 217 271 219 262 207 258 195 230 192 198 210 184 227 164 242 144 259 145 284 151 277 141 293 140 299 134 297 127 273 119 270 105
Polygon -7500403 true true -1 195 14 180 36 166 40 153 53 140 82 131 134 133 159 126 188 115 227 108 236 102 238 98 268 86 269 92 281 87 269 103 269 113

x
false
0
Polygon -7500403 true true 270 75 225 30 30 225 75 270
Polygon -7500403 true true 30 75 75 30 270 225 225 270
@#$#@#$#@
NetLogo 3D 6.0.4
@#$#@#$#@
need-to-manually-make-preview-for-this-model
@#$#@#$#@
@#$#@#$#@
@#$#@#$#@
@#$#@#$#@
default
0.0
-0.2 0 0.0 1.0
0.0 1 1.0 0.0
0.2 0 0.0 1.0
link direction
true
0
Line -7500403 true 150 150 90 180
Line -7500403 true 150 150 210 180
@#$#@#$#@
0
@#$#@#$#@
